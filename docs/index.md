---
page_title: "Official Sentry Terraform Provider"
description: |-
  Set up Sentry Team, Projects, Alerts, and more. This provider is maintained with [Sentry.io](https://sentry.io)'s sponsorship. Please add any bug reports/feature requests in the GitHub repo.
---

# Official Sentry Terraform Provider

Set up Sentry Team, Projects, Alerts, and more. This provider is maintained with [Sentry.io](https://sentry.io)'s sponsorship. Please add any bug reports/feature requests in the [GitHub repo](https://github.com/jianyuan/terraform-provider-sentry/issues).

This provider utilizes the [Web APIs](https://docs.sentry.io/api/) to interact with Sentry resources.

## Setup

### Authentication

You will need to configure the provider by providing an authentication token. You can create an authentication token within Sentry by [creating an internal integration](https://docs.sentry.io/product/integrations/integration-platform/internal-integration/#auth-tokens). This is also available for self-hosted Sentry.

```terraform
provider "sentry" {
  token = "my-auth-token"
}
```

It's best practice not to store the authentication token in plain text. As an alternative, the provider can source the authentication token from the `SENTRY_AUTH_TOKEN` environment variable. If you choose to do this, you can omit the token variable from the configuration block above.

```terraform
provider "sentry" {}
```

### Self-hosted Sentry

If you are self-hosting Sentry, you can set the base URL here. The URL format must be in the format `https://[hostname]/api/`.

```terraform
# Configure the Sentry Provider
provider "sentry" {
  base_url = "https://example.com/api/"
}
```

## Example Usage

```terraform
# Configure the Sentry Provider. Sentry will proxy most requests to the correct region based on the organization.
# To avoid the overhead of the proxy, you can configure the provider to use a specific region.
provider "sentry" {
  token = var.sentry_auth_token
}

# Configure the Sentry Provider for the US region
provider "sentry" {
  token = var.sentry_auth_token

  base_url = "https://us.sentry.io/api/"
}

# Configure the Sentry Provider for the EU region
provider "sentry" {
  token = var.sentry_auth_token

  base_url = "https://de.sentry.io/api/"
}

# Configure the Sentry Provider for self-hosted Sentry
provider "sentry" {
  token = var.sentry_auth_token

  # The URL format must be "https://[hostname]/api/".
  base_url = "https://example.com/api/"
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `base_url` (String) The target Sentry Base API URL follows the format `https://[hostname]/api/`, and this URL must end with the `/api/` path, including the trailing slash. The default value is `https://sentry.io/api/`, which proxies most requests to the appropriate region based on your organization. To avoid additional round trips, it is preferable to set the region URL explicitly. Use `https://us.sentry.io/api/` for the US region and `https://de.sentry.io/api/` for the EU region. This value is required for Sentry On-Premise deployments and can be sourced from the `SENTRY_BASE_URL` environment variable.
- `token` (String, Sensitive) The authentication token used to connect to Sentry. The value can be sourced from the `SENTRY_AUTH_TOKEN` environment variable.



## Typical Usage

Below is a full example showcasing typical usage:

```terraform
terraform {
  required_providers {
    sentry = {
      source = "jianyuan/sentry"
    }
  }
}

provider "sentry" {
  token = "xxx"
}

data "sentry_organization" "main" {
  # Taken from URL: https://sentry.io/organizations/[slug]/issues/
  slug = "my-sentry-organization"
}

output "organization" {
  value = data.sentry_organization.main
}

#
# Team
#

resource "sentry_team" "main" {
  organization = data.sentry_organization.main.id
  name         = "My team"
}

output "team" {
  value = sentry_team.main.id
}

#
# Project
#

resource "sentry_project" "main" {
  organization = sentry_team.main.organization
  team         = sentry_team.main.id
  name         = "My project"
  platform     = "python"
}

output "project" {
  value = sentry_project.main.id
}

#
# Client key
#

data "sentry_key" "main" {
  organization = sentry_project.main.organization
  project      = sentry_project.main.id

  first = true
}

output "client_key" {
  value = data.sentry_key.main
}
```
